buildscript {
}

plugins {
    id 'application'
    id 'com.diffplug.spotless' version '7.2.1'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id 'com.github.spotbugs' version '6.2.7'
    id 'org.graalvm.buildtools.native' version '0.11.0'
}

application {
    mainClass.set(System.getProperty('main_class') ?: 'Main')
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/me.gosimple/nbvcxz
    implementation 'me.gosimple:nbvcxz:1.5.1'
}

//java {
//    sourceCompatibility = JavaVersion.VERSION_17
//    targetCompatibility = JavaVersion.VERSION_17
//}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': application.getMainClass()
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude('META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA')
}

run {
    standardInput = System.in
}

graalvmNative {
    binaries.all {
        resources.autodetect()
    }
    binaries {
        main {
            // buildArgs.add("-H:+ForeignAPISupport")
            // buildArgs.add("--static-nolibc")
        }
    }
    agent {
        enabled = true
        metadataCopy {
            inputTaskNames.add("run")
            outputDirectories.add("src/main/resources/META-INF/native-image/reachability-metadata.json")
            mergeWithExisting = true
        }
        defaultMode = "standard"
    }
}
